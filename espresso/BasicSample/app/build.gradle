apply plugin: 'com.android.application'
import com.android.build.gradle.tasks.ZipAlign
import com.android.sdklib.SdkManager
import com.android.sdklib.BuildToolInfo
import com.android.sdklib.repository.local.LocalSdk

/** zipalign executable (with extension for the current OS) */
import static com.android.SdkConstants.FN_ZIPALIGN

def appDebugTestUnalignedApk = ''

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "com.example.android.testing.espresso.BasicSample"
        minSdkVersion 10
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // test apk doesn't appear in the outputs.
            // find app apk then guess the aligned apk path.
            // http://stackoverflow.com/a/26406637
            def outputFile = output.outputFile
            if (appDebugTestUnalignedApk == '' && outputFile != null && outputFile.name.endsWith('-debug.apk')) {
                appDebugTestUnalignedApk = outputFile.path.replace('.apk', '-test-unaligned.apk')
            }
        }
    }

    // app-debug-test-unaligned.apk
    // app-debug-unaligned.apk
    // app-debug.apk
    // http://stackoverflow.com/questions/22743663/in-android-gradle-how-to-define-a-task-that-only-runs-when-building-specific-bui
    tasks.whenTaskAdded { task ->
        if (task.name == 'assembleDebugTest') {
            task.dependsOn printName
        }
    }
}

// ./gradlew assembleDebugTest
task printName << {
    println "!!!! print name invoked ${appDebugTestUnalignedApk}"
    def zipAlignTask = project.tasks.create('zipalignDebugTest', ZipAlign)
    def appDebugTestApk = appDebugTestUnalignedApk.replace('-test-unaligned.apk', '-test.apk')

    println "::::::  inputFile: ${appDebugTestUnalignedApk}"
    println ":::::: outputFile: ${appDebugTestApk}"
    // class org.gradle.api.DefaultTask_Decorated
    // https://github.com/bootstraponline/tools_base/blob/d4f069cdc3d5e47649f14c8185e47b4ebc06a2c0/build-system/gradle-core/src/main/groovy/com/android/build/gradle/internal/tasks/BaseTask.groovy#L25
    println ":::::: class: ${printName.class}"

    zipAlignTask.inputFile = new File(appDebugTestUnalignedApk)
    zipAlignTask.outputFile = new File(appDebugTestApk)

    // based on https://github.com/bootstraponline/tools_base/blob/d4f069cdc3d5e47649f14c8185e47b4ebc06a2c0/build-system/integration-test/src/test/groovy/com/android/build/gradle/integration/common/utils/SdkHelper.java#L71
    def sdkFolder = project.getPlugins().getPlugin('com.android.application').sdkFolder.absolutePath
    def sdkManager = SdkManager.createManager(new LocalSdk(new File(sdkFolder)))
    def latestBuildToolFullRevision = sdkManager.getLatestBuildTool().getRevision()
    def buildToolInfo = sdkManager.getBuildTool(latestBuildToolFullRevision)
    def zipAlign = new File(buildToolInfo.getPath(BuildToolInfo.PathId.ZIP_ALIGN))
    println ":::::: zipAlign: ${zipAlign.absolutePath}"

    if (!zipAlign.exists()) {
        throw new RuntimeException("zipAlign must exist! Not found in latest build tool revision.")
    }

    zipAlignTask.zipAlignExe = zipAlign
    zipAlignTask.execute()
}

dependencies {
    // App dependencies
    compile 'com.android.support:support-annotations:21.0.3'
    compile 'com.google.guava:guava:18.0'

    // Testing-only dependencies
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.0'
}

